 01 REM EXAMPLE 6.20 FROM PROGRAMMING WITH BASIC BOOK BY BYRON S. GOTTFRIED
 02 REM ADAPTED TO TINY BASIC BY MARCO'S RETROBITS
 03 REM HTTPS://RETROBITS.ALTERVISTA.ORG    (ENGLISH)
 04 REM HTTPS://SOMEBITSOFME.ALTERVISTA.ORG (ITALIAN)
 05 REM HTTPS://RETROBITS.ITCH.IO
 06
 10 REM SIMULATION OF A GAME OF CRAPS
 20 
 30 PRINT "INPUT 1 TO QUIT, 2 TO VIEW RULES OF CRAP, ANY OTHER TO PLAY"
 31 INPUT N
 40 IF N = 1 THEN GO TO 600
 50 IF N = 2 THEN GO TO 300
 60
 70 REM SIMULATE ONE PLAY OF CRAPS
 80
 90 GO SUB 500
100 IF K-1 < 3 THEN GO TO 110
101 IF K-1 < 6 THEN GO TO 150
102 IF K-1 = 6 THEN GO TO 130
103 IF K-1 < 10 THEN GO TO 150
104 IF K-1 = 10 THEN GO TO 130
105 IF K-1 = 11 THEN GO TO 110
110 PRINT K; " - - YOU LOSE ON THE FIRST THROW"
120 GO TO 250
130 PRINT K; " - - YOU WIN ON THE FIRST THROW"
140 GO TO 250
150 LET L=K
160 PRINT K; " - - THROW THE DICE AGAIN"
170 INPUT N
180 GO SUB 500
190 IF K=L THEN GO TO 220
200 IF K=7 THEN GO TO 240
210 GO TO 160
220 PRINT K; " - - YOU WIN BY MATCHING YOUR FIRST SCORE"
230 GO TO 250
240 PRINT K; " - - YOU LOSE BY FAILING TO MATCH YOUR FIRST SCORE"
250 PRINT
260 GO TO 30
270
300 REM PRINT RULES OF CRAPS
310
320 PRINT "","RULES OF CRAPS"
330 PRINT
340 PRINT "TO WIN: OBTAIN A 7 OR 11 ON THE FIRST THROW"
350 PRINT "    OR: OBTAIN A 4,5,6,8,9 OR 10 ON THE FIRST THROW"
360 PRINT "","AND MATCH YOUR ORIGINAL SCORE BEFORE THROWING A 7"
370 PRINT
380 PRINT "TO LOSE: OBTAIN A 2,3 OR 12 ON THE FIRST THROW"
390 PRINT "     OR: OBTAIN A 4,5,6,8,9 OR 10 ON THE FIRST THROW"
400 PRINT "","AND THROW A 7 BEFORE MATCHING YOUR ORIGINAL SCORE"
410 PRINT
420 GO TO 30
500 REM THIS SUBROUTINE SIMULATES ONE THROW OF A PAIR OF DICE
520 LET M=1+RND(6)
530 LET N=1+RND(6)
540 LET K=M+N
550 RETURN
600 END
